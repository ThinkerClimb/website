<!--
 ____________________________________________________________
|                                                            |
|    DESIGN + Pat Heard { http://fullahead.org }             |
|      DATE + 2006.09.12                                     |
| COPYRIGHT + Free use if this notice is kept in place.      |
|____________________________________________________________|

-->

<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN"
    "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
<head>
  <title>司徒的教學網站</title>
  <meta http-lowast="content-type" content="application/xhtml+xml; charset=UTF-8" />
  <meta name="author" content="fullahead.org" />
  <meta name="keywords" content="XHTML, CSS, template, FullAhead" />
  <meta name="description" content="A valid, XHTML 1.0 template" />
  <meta name="robots" content="index, follow, noarchive" />
  <meta name="googlebot" content="noarchive" />
  <meta name="viewport" content="width=device-width, initial-scale=1.0, user-scalable=yes, minimum-scale=0.1, maximum-scale=100.0"/>

  <link rel="shortcut icon" href="../../../website.ico">
  <link rel="stylesheet" type="text/css" href="../../../styles/shCore.css" />
  <link rel="stylesheet" type="text/css" href="../../../styles/shThemeDefault.css" />
  <link rel="stylesheet" type="text/css" href="../../../css/html.css" media="screen, projection, tv " />
  <link rel="stylesheet" type="text/css" href="../../../css/layout.css" media="screen, projection, tv" />
  <link rel="stylesheet" type="text/css" href="../../../css/print.css" media="print" />
  
  <script type="text/javascript" src="../../../scripts/shCore.js"></script>
  <script type="text/javascript" src="../../../scripts/shBrushCpp.js"></script>

  <!-- Conditional comment to apply opacity fix for IE #content background.
       Invalid CSS, but can be removed without harming design -->
  <!--[if gt IE 5]>
  <link rel="stylesheet" type="text/css" href="css/ie.css" media="screen, projection, tv " />
  <![endif]-->
</head>

<body>
<script type="text/javascript">SyntaxHighlighter.all();</script>
<div id="wrapper">
<div id="content">
<script type="text/javascript" src="../../../header.js"></script>
<div id="page">
<h3>Windows Driver Model >> WDK 7.1 >> MASM32 >> Advanced</h3>
<p><b>StartIO</b></p>
<hr size="1">
<p>
[Github] <a href="https://github.com/steward-fu/gh_driver.git">https://github.com/steward-fu/gh_driver.git</a><br>

</p>

<p>
1. DriverStartIo<br>
2. IoMarkIrpPending<br>
3. IoStartNextPacket
</p>

<p>
main.asm
</p>

<pre class="brush:cpp">
;//========================================================================================================
;//  Basically, all of files downloaded from my website can be modified or redistributed for any purpose.
;//  It is my honor to share my interesting to everybody.
;//  If you find any illeage content out from my website, please contact me firstly.
;//  I will remove all of the illeage parts.
;//  Thanks :)
;//  
;//  Steward Fu
;//  steward.fu@gmail.com
;//  https://steward-fu.github.io/website/../../../driver.htm
;//========================================================================================================*/
.386p
.model flat, stdcall
option casemap:none
 
include c:\masm32\include\w2k\ntstatus.inc
include c:\masm32\include\w2k\ntddk.inc
include c:\masm32\include\w2k\ntoskrnl.inc
include c:\masm32\include\w2k\ntddkbd.inc
include c:\masm32\Macros\Strings.mac
includelib c:\masm32\lib\wxp\i386\ntoskrnl.lib

public DriverEntry

OurDeviceExtension struct
  pNextDev        PDEVICE_OBJECT ?
  szBuffer        BYTE 1024 dup(?)
  dwBufferLength  DWORD ?
OurDeviceExtension ends

.const
DEV_NAME word "\","D","e","v","i","c","e","\","f","i","r","s","t","S","t","a","r","t","I","O",0
SYM_NAME word "\","D","o","s","D","e","v","i","c","e","s","\","f","i","r","s","t","S","t","a","r","t","I","O",0

.code
;//*** startio routine
StartIo proc uses ebx pOurDevice:PDEVICE_OBJECT, pIrp:PIRP
  local pdx:PTR OurDeviceExtension
  local dwLen:DWORD
  local pBuf:DWORD
  
  mov eax, pOurDevice
  push (DEVICE_OBJECT PTR [eax]).DeviceExtension
  pop pdx
  
  mov eax, pIrp
  push (_IRP PTR [eax]).AssociatedIrp.SystemBuffer
  pop pBuf
  
  IoGetCurrentIrpStackLocation pIrp
  Push (IO_STACK_LOCATION PTR [eax]).Parameters.Write._Length
  pop dwLen
  movzx ebx, (IO_STACK_LOCATION PTR [eax]).MajorFunction
  .if ebx == IRP_MJ_WRITE
    invoke DbgPrint, $CTA0("StartIo: IRP_MJ_WRITE")
    mov eax, pdx
    push dwLen
    pop (OurDeviceExtension PTR [eax]).dwBufferLength
    invoke memcpy, addr (OurDeviceExtension PTR [eax]).szBuffer, pBuf, dwLen
    invoke DbgPrint, $CTA0("len: %d"), dwLen
  .elseif ebx == IRP_MJ_READ
    invoke DbgPrint, $CTA0("StartIo: IRP_MJ_READ")
    mov eax, pdx
    push (OurDeviceExtension PTR [eax]).dwBufferLength
    pop dwLen
    invoke memcpy, pBuf, addr (OurDeviceExtension PTR [eax]).szBuffer, dwLen
    invoke DbgPrint, $CTA0("buf: %s"), pBuf
  .endif
  
  mov eax, pIrp
  mov (_IRP PTR [eax]).IoStatus.Status, STATUS_SUCCESS
  push dwLen
  pop (_IRP PTR [eax]).IoStatus.Information
  fastcall IofCompleteRequest, pIrp, IO_NO_INCREMENT
  invoke IoStartNextPacket, pOurDevice, FALSE
  mov eax, STATUS_SUCCESS
  ret
StartIo endp

;//*** process file irp
IrpFile proc uses ebx pOurDevice:PDEVICE_OBJECT, pIrp:PIRP
  IoGetCurrentIrpStackLocation pIrp
  movzx ebx, (IO_STACK_LOCATION PTR [eax]).MajorFunction
  .if (ebx == IRP_MJ_CREATE) || (ebx == IRP_MJ_CLOSE) 
    .if (ebx == IRP_MJ_CREATE)
      invoke DbgPrint, $CTA0("IrpFile: IRP_MJ_CREATE")
    .else
      invoke DbgPrint, $CTA0("IrpFile: IRP_MJ_CLOSE")
    .endif
    mov eax, pIrp
    and (_IRP PTR [eax]).IoStatus.Information, 0
    mov (_IRP PTR [eax]).IoStatus.Status, STATUS_SUCCESS
    fastcall IofCompleteRequest, pIrp, IO_NO_INCREMENT
    mov eax, STATUS_SUCCESS
  .elseif (ebx == IRP_MJ_READ) || (ebx == IRP_MJ_WRITE)
    IoGetCurrentIrpStackLocation pIrp
    .if (ebx == IRP_MJ_READ)
      invoke DbgPrint, $CTA0("IrpFile: IRP_MJ_READ")
    .else
      invoke DbgPrint, $CTA0("IrpFile: IRP_MJ_WRITE")
    .endif
    IoMarkIrpPending pIrp
    invoke IoStartPacket, pOurDevice, pIrp, 0, NULL
    mov eax, STATUS_PENDING
  .endif
  ret
IrpFile endp

;//*** process pnp irp
IrpPnP proc uses ebx pDevObj:PDEVICE_OBJECT, pIrp:PIRP
  local pdx:PTR OurDeviceExtension
  local szSymName:UNICODE_STRING

  mov eax, pDevObj
  push (DEVICE_OBJECT PTR [eax]).DeviceExtension
  pop pdx
   
  IoGetCurrentIrpStackLocation pIrp
  movzx ebx, (IO_STACK_LOCATION PTR [eax]).MinorFunction
  .if ebx == IRP_MN_START_DEVICE
    mov eax, pIrp
    mov (_IRP PTR [eax]).IoStatus.Status, STATUS_SUCCESS
  .elseif ebx == IRP_MN_REMOVE_DEVICE
    invoke RtlInitUnicodeString, addr szSymName, offset SYM_NAME
    invoke IoDeleteSymbolicLink, addr szSymName     
    mov eax, pIrp
    mov (_IRP PTR [eax]).IoStatus.Status, STATUS_SUCCESS

    mov eax, pdx
    invoke IoDetachDevice, (OurDeviceExtension PTR [eax]).pNextDev
    invoke IoDeleteDevice, pDevObj
  .endif
  IoSkipCurrentIrpStackLocation pIrp

  mov eax, pdx
  invoke IoCallDriver, (OurDeviceExtension PTR [eax]).pNextDev, pIrp
  ret
IrpPnP endp

;//*** system will vist this routine when it needs to add new device
AddDevice proc pOurDriver:PDRIVER_OBJECT, pPhyDevice:PDEVICE_OBJECT
  local pOurDevice:PDEVICE_OBJECT
  local suDevName:UNICODE_STRING
  local szSymName:UNICODE_STRING

  invoke DbgPrint, $CTA0("MASM32 WDM driver tutorial for StartIO")
  invoke RtlInitUnicodeString, addr suDevName, offset DEV_NAME
  invoke RtlInitUnicodeString, addr szSymName, offset SYM_NAME

  invoke IoCreateDevice, pOurDriver, sizeof OurDeviceExtension, addr suDevName, FILE_DEVICE_UNKNOWN, 0, FALSE, addr pOurDevice
  .if eax == STATUS_SUCCESS
    invoke IoAttachDeviceToDeviceStack, pOurDevice, pPhyDevice
    .if eax != NULL
      push eax
      mov eax, pOurDevice
      mov eax, (DEVICE_OBJECT PTR [eax]).DeviceExtension
      pop (OurDeviceExtension PTR [eax]).pNextDev

      mov eax, pOurDevice
      or (DEVICE_OBJECT PTR [eax]).Flags, DO_BUFFERED_IO
      and (DEVICE_OBJECT PTR [eax]).Flags, not DO_DEVICE_INITIALIZING
      mov eax, STATUS_SUCCESS
    .else
      mov eax, STATUS_UNSUCCESSFUL
    .endif      
    invoke IoCreateSymbolicLink, addr szSymName, addr suDevName
  .endif
  ret
AddDevice endp
  
;//*** it is time to unload our driver
Unload proc pOurDriver:PDRIVER_OBJECT
  xor eax, eax
  ret
Unload endp

;//*** driver entry
DriverEntry proc pOurDriver:PDRIVER_OBJECT, pOurRegistry:PUNICODE_STRING
  mov eax, pOurDriver
  mov (DRIVER_OBJECT PTR [eax]).MajorFunction[IRP_MJ_PNP    * (sizeof PVOID)], offset IrpPnP
  mov (DRIVER_OBJECT PTR [eax]).MajorFunction[IRP_MJ_CREATE * (sizeof PVOID)], offset IrpFile
  mov (DRIVER_OBJECT PTR [eax]).MajorFunction[IRP_MJ_CLOSE  * (sizeof PVOID)], offset IrpFile
  mov (DRIVER_OBJECT PTR [eax]).MajorFunction[IRP_MJ_WRITE  * (sizeof PVOID)], offset IrpFile
  mov (DRIVER_OBJECT PTR [eax]).MajorFunction[IRP_MJ_READ   * (sizeof PVOID)], offset IrpFile
  mov (DRIVER_OBJECT PTR [eax]).DriverStartIo, offset StartIo
  mov (DRIVER_OBJECT PTR [eax]).DriverUnload, offset Unload
  mov eax, (DRIVER_OBJECT PTR [eax]).DriverExtension
  mov (DRIVER_EXTENSION PTR [eax]).AddDevice, AddDevice
  mov eax, STATUS_SUCCESS
  ret
DriverEntry endp
end DriverEntry
.end
</pre>

<p>
startio.inf
</p>

<pre class="brush:cpp">
;//========================================================================================================
;//  Basically, all of files downloaded from my website can be modified or redistributed for any purpose.
;//  It is my honor to share my interesting to everybody.
;//  If you find any illeage content out from my website, please contact me firstly.
;//  I will remove all of the illeage parts.
;//  Thanks :)
;//  
;//  Steward Fu
;//  steward.fu@gmail.com
;//  https://steward-fu.github.io/website/../../../driver.htm
;//========================================================================================================*/
[Version]
Signature=$CHICAGO$
Class=Unknown
Provider=%MFGNAME%
DriverVer=12/23/2016,1.0.0.0
   
[DestinationDirs]
DefaultDestDir=10,System32\Drivers
   
[Manufacturer]
%MFGNAME%=DeviceList
   
[SourceDisksFiles]
main.sys=1,,,
   
[SourceDisksNames]
1=%INSTDISK%,,,
   
[DeviceList]
%DESCRIPTION%=DriverInstall, *firstStartIO
   
[DriverInstall.NT]
CopyFiles=DriverCopyFiles
   
[DriverCopyFiles]
main.sys,,,2
   
[DriverInstall.NT.Services]
AddService=FILEIO,2,DriverService
   
[DriverService]
ServiceType=1
StartType=3
ErrorControl=1
ServiceBinary=%10%\system32\drivers\main.sys
   
[DriverInstall.NT.HW]
AddReg=DriverHwAddReg
   
[DriverHwAddReg]
HKR,,SampleInfo,,""
   
[DriverInstall]
AddReg=DriverAddReg
CopyFiles=DriverCopyFiles
   
[DriverAddReg]
HKR,,DevLoader,,*ntkern
HKR,,NTMPDriver,,main.sys
   
[DriverInstall.HW]
AddReg=DriverHwAddReg
   
[Strings]
MFGNAME="firstStartIO"
INSTDISK="firstStartIO Disc"
DESCRIPTION="firstStartIO(Assembly)"
</pre>

<p>
<img src="asm_startio/1.jpg" class="maxSize">
</p>

<p>
<img src="asm_startio/2.jpg" class="maxSize">
</p>

<p>
<img src="asm_startio/3.jpg" class="maxSize">
</p>

<p>
<img src="asm_startio/4.jpg" class="maxSize">
</p>

<p>
app.asm
</p>

<pre class="brush:cpp">
;//========================================================================================================
;//  Basically, all of files downloaded from my website can be modified or redistributed for any purpose.
;//  It is my honor to share my interesting to everybody.
;//  If you find any illeage content out from my website, please contact me firstly.
;//  I will remove all of the illeage parts.
;//  Thanks :)
;//  
;//  Steward Fu
;//  steward.fu@gmail.com
;//  https://steward-fu.github.io/website/../../../driver.htm
;//========================================================================================================*/
.386p
.model flat, stdcall
option casemap:none

include c:\masm32\include\windows.inc
include c:\masm32\include\masm32.inc
include c:\masm32\include\user32.inc
include c:\masm32\include\msvcrt.inc
include c:\masm32\Macros\Strings.mac
include c:\masm32\include\kernel32.inc

includelib c:\masm32\lib\user32.lib
includelib c:\masm32\lib\masm32.lib
includelib c:\masm32\lib\msvcrt.lib
includelib c:\masm32\lib\kernel32.lib

.const
DEV_NAME        db "\\.\firstStartIO",0
ERR_OPEN_DRIVER db "failed to open firstStartIO driver !",0
SEND_MSG        db "this is test message !",0
WRITE_MSG       db "Write: %s",10,13,0
READ_MSG        db "Read: %s",10,13,0
LEN_MSG         db "Length: %d",10,13,0

.data?
hFile     dd ?
dwRet     dd ?
szBuffer  db  256 dup(?)

.code
start:
  invoke CreateFile, offset DEV_NAME, GENERIC_READ or GENERIC_WRITE, FILE_SHARE_READ, 0, OPEN_EXISTING, FILE_ATTRIBUTE_NORMAL, 0
  .if eax == INVALID_HANDLE_VALUE
    invoke crt_printf, offset ERR_OPEN_DRIVER
    invoke ExitProcess, 0
  .endif
  mov hFile, eax

  invoke wsprintf, offset szBuffer, offset SEND_MSG
  invoke StrLen, offset szBuffer
  mov dwRet, eax
  invoke crt_printf, offset WRITE_MSG, offset szBuffer
  invoke crt_printf, offset LEN_MSG, dwRet
  invoke WriteFile, hFile, offset szBuffer, dwRet, offset dwRet, 0
  invoke crt_memset, offset szBuffer, 0, 255
  invoke ReadFile, hFile, offset szBuffer, 255, offset dwRet, 0
  invoke crt_printf, offset READ_MSG, offset szBuffer
  invoke crt_printf, offset LEN_MSG, dwRet
  invoke CloseHandle, hFile

  invoke ExitProcess, 0
end start
</pre>

<p>
<img src="asm_startio/5.jpg" class="maxSize">
</p>

<p>
<img src="asm_startio/6.jpg" class="maxSize">
</p>

<p>
<br><a href="../../../driver.htm">返回上一頁</a>
</p>

</div>
</div>
</div>
</body>
</html>

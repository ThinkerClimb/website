<!--
 ____________________________________________________________
|                                                            |
|    DESIGN + Pat Heard { http://fullahead.org }             |
|      DATE + 2006.09.12                                     |
| COPYRIGHT + Free use if this notice is kept in place.      |
|____________________________________________________________|

-->

<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN"
    "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
<head>
  <title>司徒的教學網站</title>
  <meta http-lowast="content-type" content="application/xhtml+xml; charset=UTF-8" />
  <meta name="author" content="fullahead.org" />
  <meta name="keywords" content="XHTML, CSS, template, FullAhead" />
  <meta name="description" content="A valid, XHTML 1.0 template" />
  <meta name="robots" content="index, follow, noarchive" />
  <meta name="googlebot" content="noarchive" />
  <meta name="viewport" content="width=device-width, initial-scale=1.0, user-scalable=yes, minimum-scale=0.1, maximum-scale=100.0"/>

  <link rel="shortcut icon" href="../../../../website.ico">
  <link rel="stylesheet" type="text/css" href="../../../../styles/shCore.css" />
  <link rel="stylesheet" type="text/css" href="../../../../styles/shThemeDefault.css" />
  <link rel="stylesheet" type="text/css" href="../../../../css/html.css" media="screen, projection, tv " />
  <link rel="stylesheet" type="text/css" href="../../../../css/layout.css" media="screen, projection, tv" />
  <link rel="stylesheet" type="text/css" href="../../../../css/print.css" media="print" />
  
  <script type="text/javascript" src="../../../../scripts/shCore.js"></script>
  <script type="text/javascript" src="../../../../scripts/shBrushCpp.js"></script>

  <!-- Conditional comment to apply opacity fix for IE #content background.
       Invalid CSS, but can be removed without harming design -->
  <!--[if gt IE 5]>
  <link rel="stylesheet" type="text/css" href="css/ie.css" media="screen, projection, tv " />
  <![endif]-->
</head>

<body>
<script type="text/javascript">SyntaxHighlighter.all();</script>
<div id="wrapper">
<div id="content">
<script type="text/javascript" src="../../../../header.js"></script>
<div id="page">
<h3>Linux Device Driver >> ARM CPU >> Assembly >> Advanced >> Char (Kernel 2.4)</h3>
<p><b>IOCTL</b></p>
<hr size="1">
<p>
[Github] <a href="https://github.com/steward-fu/gh_driver.git">https://github.com/steward-fu/gh_driver.git</a>
</p>

<p>
Kernel程式
</p>

<pre>
$ vim main.S
</pre>

<pre class="brush:cpp">
#//===========================================================================
#// Linux Device Driver for ARM in Assembly
#// This file can be distributed or modified under GPL license
#// http://steward-fu.github.io/website/../../../../driver.htm
#// Steward Fu 2016/02/17
#//===========================================================================
.include "main.inc"
.global init_module
.global cleanup_module

.section .modinfo
__module_kernel_version: .asciz "kernel_version=2.6.28";    .align 
__module_license:        .asciz "license=GPL";              .align 
__module_author:         .asciz "author=Steward";           .align 
__module_description:    .asciz "description=Linux Driver"; .align

.section .data
dev_no:   .zero          4; .align
kdev:     .zero          4; .align
major:    .zero          1; .align
fops:     .zero _fops_size; .align

.section .rodata
THIS_MODULE: .word  __this_module; .align
dev_name:    .asciz "lddia";       .align

.section .text
#//===========================================================================
#// description:
#//  open() function for file operation
#//
#// parameter:
#//  r0
#//   struct inode* inode
#//  r1
#//   struct file* flip
#//
#// return:
#//  errno
#//===========================================================================
lddia_open:
  push {lr}
  # printk()
  _push _r0, _sz, "lddia, open\n"
  _call printk

  # return 0
  mov r0, #0
  pop {pc}

#//===========================================================================
#// description:
#//  close() function for file operation
#//
#// parameter:
#//  r0
#//   struct inode* inode
#//  r1
#//   struct file* flip
#//
#// return:
#//  errno
#//===========================================================================
lddia_close:
  push {lr}
  # printk()
  _push _r0, _sz, "lddia, close\n"
  _call printk

  # return 0
  mov r0, #0
  pop {pc}

#//===========================================================================
#// description:
#//  ioctl() function for file operation
#//
#// parameter:
#//  r0
#//   struct inode* inode
#//  r1
#//   struct file* filp
#//  r2
#//   unsigned int cmd
#//  r3
#//   unsigned long arg
#//
#// return:
#//  read length
#//===========================================================================
lddia_ioctl:
  push {lr}
  # printk()
  mov r1, r2
  mov r2, r3
  _push _r0, _sz, "lddia, ioctl cmd:0x%X, arg:0x%X\n"
  _call printk

  # return 0
  mov r0, #0
  pop {pc}

#//===========================================================================
#// description:
#//  unload driver
#//
#// parameter:
#//  none
#//
#// return:
#//  nothing
#//===========================================================================
cleanup_module:
  push {lr}
  .if _use_kernel == 24
    # unregister(major, dev_name)
    _push _r1, _va, dev_name
    _push _r0, _vv, major
    _call unregister_chrdev
  .else
    # cdev_del(kdev)
    _push _r0, _vv, kdev
    _call cdev_del

    # unregister_chrdev_region(dev_no, 1)
    _push _r1, _dw, 1
    _push _r0, _vv, dev_no
    _call unregister_chrdev_region
  .endif
  # printk()
  _push _r0, _sz, "lddia, unload it!\n"
  _call printk
  pop {pc}

#//===========================================================================
#// description:
#//  load driver
#//
#// parameter:
#//  none
#//
#// return:
#//  errno
#//===========================================================================
init_module:
  push {lr}
  # printk()
  _push _r0, _sz, "lddia, load it!\n"
  _call printk

  # fops.owner = THIS_MODULE
  # fops.open  = lddia_open
  # fops.close = lddia_close
  # fopd.ioctl = lddia_ioctl
  _set_fops_struct fops, _fops_owner,   THIS_MODULE
  _set_fops_struct fops, _fops_open,    lddia_open
  _set_fops_struct fops, _fops_release, lddia_close
  _set_fops_struct fops, _fops_ioctl,   lddia_ioctl

  .if _use_kernel == 24
    # printk()
    _push _r0, _sz, "lddia, kernel 2.4\n"
    _call printk

    # major = register_chrdev(0, dev_name, &amp;ops)
    _push _r2, _va, fops
    _push _r1, _va, dev_name
    _push _r0, _dw, 0
    _call register_chrdev
    ldr r1, =major
    str r0, [r1]
  .else 
    # printk()
    _push _r0, _sz, "lddia, kernel 2.6\n"
    _call printk

    # alloc_chrdev_region(&amp;dev_no, 0, 1, dev_name)
    _push _r3, _va, dev_name
    _push _r2, _dw, 1
    _push _r1, _dw, 0
    _push _r0, _va, dev_no
    _call alloc_chrdev_region

    # kdev = cdev_alloc()
    _call cdev_alloc
    ldr r1, =kdev
    str r0, [r1]

    # kdev->owner = THIS_MODULE
    # kdev->ops   = &amp;fops
    _set_cdev_struct kdev, _cdev_owner, THIS_MODULE
    _set_cdev_struct kdev, _cdev_fops, fops

    # cdev_add(kdev, dev_no, 1)
    _push _r2, _dw, 1
    _push _r1, _vv, dev_no 
    _push _r0, _vv, kdev
    _call cdev_add

    # major = MAJOR(dev_no)
    MAJOR dev_no
    ldr r1, =major
    str r0, [r1]
  .endif
  # printk()
  _push _r1, _vv, major
  _push _r0, _sz, "lddia, major number is %d\n"
  _call printk

  # return 0
  mov r0, #0
  pop {pc}
 .end
</pre>

<p>
Include程式
</p>

<pre>
$ vim main.inc
</pre>

<pre class="brush:cpp">
#//===========================================================================
#// Linux Device Driver for ARM in Assembly
#// This file can be distributed or modified under GPL license
#// http://steward-fu.github.io/website/../../../../driver.htm
#// Steward Fu 2016/02/17
#//===========================================================================
# 24 -> 2.4
# 26 -> 2.6
.equiv _use_kernel,             26

# parameter for open() function
.equiv O_RDONLY,                0
.equiv O_WRONLY,                1
.equiv O_RDWR,                  2

# file operations definition
.equiv _fops_owner,             0
.equiv _fops_llseek,            1
.equiv _fops_read,              2
.equiv _fops_write,             3
.equiv _fops_aio_read,          4
.equiv _fops_aio_write,         5
.equiv _fops_readdir,           6
.equiv _fops_poll,              7
.equiv _fops_ioctl,             8
.equiv _fops_unlocked_ioctl,    9
.equiv _fops_compat_ioctl,      10
.equiv _fops_mmap,              11
.equiv _fops_open,              12
.equiv _fops_flush,             13
.equiv _fops_release,           14
.equiv _fops_fsync,             15
.equiv _fops_aio_fsync,         16
.equiv _fops_fasync,            17
.equiv _fops_lock,              18
.equiv _fops_sendpage,          19
.equiv _fops_get_unmapped_area, 20
.equiv _fops_check_flags,       21
.equiv _fops_dir_notify,        22
.equiv _fops_flock,             23
.equiv _fops_splice_write,      24
.equiv _fops_splice_read,       25
.equiv _fops_setlease,          26
.equiv _fops_index,             27
.equiv _fops_size,              (_fops_index &lt;&lt; 2)

# cdev definition
.equiv _cdev_owner,     9
.equiv _cdev_fops,      10
.equiv _cdev_size,      (_kobj_size + 4 + 4 + _list_head_size + 4 + 4)
.equiv _kobj_size,      36
.equiv _list_head_size, 8

# parameter for _push macro
.equiv _r0, 0  @ r0 register
.equiv _r1, 1  @ r1 register
.equiv _r2, 2  @ r2 register
.equiv _r3, 3  @ r3 register
.equiv _sp, 4  @ sp register
.equiv _sz, 5  @ string with zero char
.equiv _dw, 6  @ data word
.equiv _db, 7  @ data byte
.equiv _va, 8  @ variable address
.equiv _vv, 9  @ variable value
.equiv _rv, 10 @ register value

# retrive from linux header file
# please keep original name
.equiv MINORBITS,      20
.equiv MINORMASK,      ((1 &lt;&lt; MINORBITS) - 1)
.equiv _IOC_NRBITS,    8
.equiv _IOC_TYPEBITS,  8
.equiv _IOC_SIZEBITS,  14
.equiv _IOC_DIRBITS,   2
.equiv _IOC_NRMASK,    ((1 &lt;&lt; _IOC_NRBITS) - 1)
.equiv _IOC_TYPEMASK,  ((1 &lt;&lt; _IOC_TYPEBITS) - 1)
.equiv _IOC_SIZEMASK,  ((1 &lt;&lt; _IOC_SIZEBITS) - 1)
.equiv _IOC_DIRMASK,   ((1 &lt;&lt; _IOC_DIRBITS) - 1)
.equiv _IOC_NRSHIFT,   0
.equiv _IOC_TYPESHIFT, (_IOC_NRSHIFT + _IOC_NRBITS)
.equiv _IOC_SIZESHIFT, (_IOC_TYPESHIFT + _IOC_TYPEBITS)
.equiv _IOC_DIRSHIFT,  (_IOC_SIZESHIFT + _IOC_SIZEBITS)
.equiv _IOC_NONE,      0
.equiv _IOC_WRITE,     1
.equiv _IOC_READ,      2

# #define MAJOR(dev) ((unsigned int) ((dev) MINORBITS))
.macro MAJOR arg:req
  ldr r0, =\arg
  ldr r0, [r0]
  lsr r0, r0, #MINORBITS 
.endm

# #define MINOR(dev) ((unsigned int) ((dev) &amp; MINORMASK))
.macro MINOR arg:req
  ldr r0, =\arg
  ldr r0, [r0]
  and r0, r0, #MINORMASK
.endm

# #define MKDEV(ma,mi) (((ma) &lt;&lt; MINORBITS) | (mi))
.macro MKDEV ma:req, mi:req
  mov r1, #mi
  mov r0, #ma
  lsl r0, #MINORBITS
  or r0, r0, r1
.endm

# emulate push instruction
# howto: _push param1, param2, param3
#  param1: where (push to register or stack)
#    _r0, _r1, _r2, _r3: push to register
#    _sp: push to stack
#  param2: param3's data type
#    _sz: string
#    _dw: word
#    _db: byte
#    _va: variable address
#    _vv: variable value
#    _rv: register value
#  param3: data
.macro _push where:req, type:req, arg:req
  .if \type == _sz
    b l_push_sz\@
    _tmp\@: .asciz "\arg"; .align
    l_push_sz\@:
      ldr r0, =_tmp\@
  .elseif \type == _dw
    b l_push_dw\@
    _tmp\@: .word \arg; .align
    l_push_dw\@:
      ldr r0, =_tmp\@
      ldr r0, [r0]
  .elseif \type == _db
    b l_push_db\@
    _tmp\@: .byte \arg; .align
    l_push_db\@:
      ldr r0, =_tmp\@
      ldr r0, [r0]
  .elseif \type == _va
    ldr r0, =\arg
  .elseif \type == _vv
    ldr r0, =\arg
    ldr r0, [r0]
  .elseif \type == _rn
    mov r0, \arg
  .else
    .exitm
  .endif
  .if \where == _r0
  .elseif \where == _r1
    mov r1, r0
  .elseif \where == _r2
    mov r2, r0
  .elseif \where == _r3
    mov r3, r0
  .elseif \where == _sp
    push {r0}
  .else
    .exitm
  .endif
.endm

# emulate call instruction
# it includes import(.extern) declaration
.macro _call fun:req
  .extern \fun
  bl \fun
.endm

# set the item of file operation structure
# howto: _set_fops_struct param1, param2, param3
#  param1: pointer to fops strcuture
#  param2: which item to set
#  param3: data
# example: _set_fops_struct fops, _fops_open, open_function
# (in c/cpp) =&gt; struct file_operations fops = {.open = open_function};
.macro _set_fops_struct base:req, index:req, addr:req
  ldr r2, =\addr
  ldr r0, =\base
  mov r1, #\index
  add r0, r0, r1, lsl #2
  str r2, [r0]
.endm

# set the item of cdev structure
# howto: _set_cdev_struct param1, param2, param3
#  param1: pointer to cdev strcuture
#  param2: which item to set
#  param3: data
# example: _set_cdev_struct kdev, _cdev_ops, ops
# (in c/cpp) =&gt; struct cdev kdev = {.ops = &ops};
.macro _set_cdev_struct base:req, index:req, addr:req
  ldr r2, =\addr
  ldr r0, =\base
  ldr r0, [r0]
  mov r1, #\index
  add r0, r0, r1, lsl #2
  str r2, [r0]
.endm

# #define _IOC(dir, type, nr, size) \
#   (((dir)  &lt;&lt; _IOC_DIRSHIFT)  | \
#   (((type) &lt;&lt; _IOC_TYPESHIFT) | \
#   (((nr)   &lt;&lt; _IOC_NRSHIFT)   | \
#   (((size) &lt;&lt; _IOC_SIZESHIFT))
.macro _IOC dir:req, type:req, nr:req, size:req
  mov r0, #\dir
  mov r1, #\type
  mov r2, #\nr
  mov r3, #\size
  lsl r0, r0, #_IOC_DIRSHIFT
  lsl r1, r1, #_IOC_TYPESHIFT
  lsl r2, r2, #_IOC_NRSHIFT
  lsl r3, r3, #_IOC_SIZESHIFT
  orr r0, r0, r1
  orr r0, r0, r2
  orr r0, r0, r3
.endm

# #define _IO(type, nr) \
#   _IOC(_IOC_NONE, (type), (nr), 0)
.macro _IO type:req, nr:req
  _IOC _IOC_NONE, \type, \nr, 0
.endm

# #define _IOR(type, nr, size) \
#   _IOC(_IOC_READ, (type), (nr), sizeof(size))
.macro _IOR type:req, nr:req, size:req
  _IOC _IOC_READ, \type, \nr, \size
.endm

# #define _IOW(type, nr, size) \
#   _IOC(_IOC_WRITE, (type), (nr), sizeof(size))
.macro _IOW type:req, nr:req, size:req
  _IOC _IOC_WRITE, \type, \nr, \size
.endm

# #define _IOWR(type, nr, size) \
#   _IOC(_IOC_READ|_IOC_WRITE, (type), (nr), sizeof(size))
.macro _IOWR type:req, nr:req, size:req
  _IOC _IOC_READ | _IOC_WRITE, \type, \nr, \size
.endm

</pre>

<p>
Application程式
</p>

<pre>
$ vim app.S
</pre>

<pre class="brush:cpp">
#//===========================================================================
#// Linux Application for ARM in Assembly
#// This file can be distributed or modified under GPL license
#// http://steward-fu.github.io/website/../../../../driver.htm
#// Steward Fu 2016/02/17
#//===========================================================================
.include "main.inc"
.equiv _APP_BASE, 0

.section .data
fd: .zero 4; .align

.section .text
dev_name: .asciz "/dev/lddia"; .align

.section .text
.global main
#//===========================================================================
#// description:
#//  main entry point
#//
#// parameter:
#//  r0
#//   argc
#//  r1
#//   argv
#//
#// return:
#//  errno
#//===========================================================================
main:
  push {lr}
  # printf()
  _push _r0, _sz, "app, run it!\n"
  _call printf

  # fd = open("lddia", O_RDWR)
  _push _r1, _dw, O_RDWR
  _push _r0, _va, dev_name
  _call open
  ldr r1, =fd
  str r0, [r1]

  # if fd == -1 then exit
  cmp r0, #-1
  bne do_ioctl

  # printf()
  _push _r0, _sz, "app, failed to open lddia\n"
  _call printf

  # exit(1)
  _push _r0, _dw, 1
  _call exit

do_ioctl:
  #define _APP_IOCTL_CMD _IO(_APP_BASE, 0)
  _IO _APP_BASE, 0
  push {r0}

  # ioctl(fd, _APP_IOCTL_CMD, 0)
  mov r1, r0
  _push _r2, _dw, 0
  _push _r0, _vv, fd
  _call ioctl

  # printf()
  pop {r1}
  _push _r0, _sz, "app, ioctl _APP_IOCTL_CMD(0x%X)\n"
  _call printf

  #define _APP_IOCTL_SET _IOR(_APP_BASE, 1, int)
  _IOR _APP_BASE, 1, 4
  push {r0}

  # ioctl(fd, _APP_IOCTL_SET, 1)
  mov r1, r0
  _push _r2, _dw, 1
  _push _r0, _vv, fd
  _call ioctl

  # printf()
  pop {r1}
  _push _r0, _sz, "app, ioctl _APP_IOCTL_SET(0x%X)\n"
  _call printf

  #define _APP_IOCTL_GET _IOW(_APP_BASE, 2, int)
  _IOW _APP_BASE, 2, 4
  push {r0}

  # ioctl(fd, _APP_IOCTL_GET, 2)
  mov r1, r0
  _push _r2, _dw, 2
  _push _r0, _vv, fd
  _call ioctl

  # printf()
  pop {r1}
  _push _r0, _sz, "app, ioctl _APP_IOCTL_GET(0x%X)\n"
  _call printf

  #define _APP_IOCTL_BID _IOWR(_APP_BASE, 3, int)
  _IOWR _APP_BASE, 3, 4
  push {r0}

  # ioctl(fd, _APP_IOCTL_BID, 3)
  mov r1, r0
  _push _r2, _dw, 3
  _push _r0, _vv, fd
  _call ioctl

  # printf()
  pop {r1}
  _push _r0, _sz, "app, ioctl _APP_IOCTL_BID(0x%X)\n"
  _call printf

  # close
  _push _r0, _vv, fd
  _call close

  # printf
  _push _r0, _sz, "app, exit!\n"
  _call printf

  # return 0
  mov r0, #0
  pop {pc}
 .end
</pre>

<p>
新增Makefile編譯檔案(檔案名稱的大小寫是固定的)
</p>

<pre>
$ vim Makefile
obj-m += ldd.o
ldd-objs:= main.o
all:
    as -o app.o app.S
    gcc -o app app.o
    as -o main.o main.S
    make -C /lib/modules/$(shell uname -r)/build M=$(PWD) modules
clean:
    make -C /lib/modules/$(shell uname -r)/build M=$(PWD) clean
</pre>

<p>
make指令的前面有一個Tab字元，請不要使用空白鍵代替，不然會無法編譯驅動程式
</p>

<p>
接著輸入<b>make</b>指令進行編譯就可以產生Linux驅動程式檔案(main.ko)
</p>

<pre>
$ make
as -o app.o app.S
gcc -o app app.o
as -o main.o main.S
make -C /lib/modules/2.6.28.10-power53/build M=/home/user/Data/n900/ldd/asm/char/ioctl modules
make[1]: Entering directory `/usr/src/2.6.28.10-power54'
  LD [M]  /home/user/Data/n900/ldd/asm/char/ioctl/ldd.o
  Building modules, stage 2.
  MODPOST 1 modules
  LD [M]  /home/user/Data/n900/ldd/asm/char/ioctl/ldd.ko
make[1]: Leaving directory `/usr/src/2.6.28.10-power54'
</pre>

<p>
編譯完成後(Linux Kernel 2.6的驅動程式附檔名為<b>*.ko</b>, Linux Kernel 2.4的驅動程式附檔名為<b>*.o</b>)
</p>

<p>
載入驅動程式(insmod)和查看Debug訊息(dmesg)
</p>

<pre>
$ sudo insmod ldd.ko
$ dmesg
[20484.327087] lddia, load it!
[20484.327117] lddia, kernel 2.6
[20484.327148] lddia, major number is 246
</pre>

<p>
裝置資訊
</p>

<pre>
$ cat /proc/devices
Character devices:
 ...
246 lddia
 ...
</pre>

<p>
建立Symbolic
</p>

<pre>
$ sudo mknod /dev/lddia c 246 0
$ sudo chmod 0777 /dev/lddia
</pre>

<p>
測試
</p>

<pre>
$ ./app
app, run it!
app, ioctl _APP_IOCTL_CMD(0x0)
app, ioctl _APP_IOCTL_SET(0x40040002)
app, ioctl _APP_IOCTL_GET(0x80040001)
app, ioctl _APP_IOCTL_BID(0xC0040003)
app, exit!

$ dmesg
[20513.113677] lddia, open
[20513.113769] lddia, ioctl cmd:0x0, arg:0x0
[20513.114166] lddia, ioctl cmd:0x40040002, arg:0x1
[20513.114440] lddia, ioctl cmd:0x80040001, arg:0x2
[20513.114746] lddia, ioctl cmd:0xC0040003, arg:0x3
[20513.114990] lddia, close
</pre>

<p>
刪除Symbolic
</p>

<pre>
$ sudo rm /dev/lddia
</pre>

<p>
卸載驅動程式(rmmod)和查看Debug訊息(dmesg)
</p>

<pre>
$ sudo rmmod ldd
$ dmesg
[20527.734985] lddia, unload it!
</pre>

<p>
<br><a href="../../../../driver.htm">返回上一頁</a>
</p>

</div>
</div>
</div>
</body>
</html>
